<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute"
                width="100%"
                height="100%">
  <mx:Panel title="Layer Visibility"
            width="100%"
            height="100%">
    <mx:HBox width="100%">
      <mx:VBox id="toc"
               width="150"
               height="600"
               enabled="false">
      </mx:VBox>
      <maps:Map xmlns:maps="com.google.maps.*"
                id="map"
                mapevent_mapready="onMapReady(event)"
                width="100%"
                height="100%"
				sensor="false" key="ABQIAAAAGY8Xhrw1zw6Ia3dwUJVxKRT2yXp_ZAY8_ufC3CFXhHIE1NvwkxQTxMd7f8tzuEeq7aWKejTn-XQrIA"/>

    </mx:HBox>

  </mx:Panel>

  <mx:Script>
    <![CDATA[
      import mx.controls.CheckBox;
      import com.google.maps.LatLngBounds;
      import com.google.maps.controls.NavigationControl;
      import com.google.maps.controls.MapTypeControl;

      import com.google.maps.LatLng;
      import com.google.maps.Map;
      import com.google.maps.MapEvent;
      import com.google.maps.MapType;
      import com.google.maps.MapOptions;
      import com.google.maps.interfaces.*;

      import com.google.maps.extras.arcgislink.*;

      private var dynaLayer:ArcGISMapOverlay;


      private function onMapReady(event:Event):void {
        map.addControl(new com.google.maps.controls.MapTypeControl());
        map.addControl(new NavigationControl());
        map.enableScrollWheelZoom();
        map.enableContinuousZoom();
        // var bnds:LatLngBounds=new LatLngBounds(new LatLng(36.8,-103),new LatLng(40.2,-94));
        map.setCenter(new LatLng(40, -95), 4);
        dynaLayer=new ArcGISMapOverlay("http://sampleserver1.arcgisonline.com/ArcGIS/rest/services/Specialty/ESRI_StateCityHighway_USA/MapServer", new ArcGISMapOverlayOptions({alpha: 0.5}));
        dynaLayer.addEventListener(ServiceEvent.LOAD, function(evt:Event):void {
            map.addOverlay(dynaLayer);
            var service:MapService=dynaLayer.getMapService();
            var layers:Array=service.getLayers();
            for (var i:int=0; i < layers.length; i++) {
              var layer:Layer=layers[i] as Layer;
              var chk:CheckBox=new CheckBox();
              chk.id='layer' + layer.id;
              chk.selected=layer.visible;
              chk.label=layer.name;
              chk.name='layer' + layer.id;
              chk.addEventListener(MouseEvent.CLICK, setVis);
              toc.addChild(chk);
            }

          });
        dynaLayer.addEventListener(ServiceEvent.EXPORTMAP_LOAD, function(evt:ServiceEvent):void {
            toc.enabled=true;
          });



      }

      private function setVis(evt:Event):void {
        var service:MapService=dynaLayer.getMapService();
        var layers:Array=service.getLayers();
        for (var i:int=0; i < layers.length; i++) {
          var layer:Layer=layers[i] as Layer;
          var chk:CheckBox=toc.getChildByName('layer' + layer.id) as CheckBox;
          if (chk != null) {
            layer.visible=chk.selected;
          }
        }
        dynaLayer.refresh();
      }
    ]]>
  </mx:Script>
</mx:Application>
